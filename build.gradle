plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.osy'
version = '0.0.1-SNAPSHOT'
description = 'share-todo'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

def queryDslVersion = "7.0"
def querydslSrcDir  = layout.buildDirectory.dir("generated/querydsl").get().asFile // build/generated/querydsl
dependencies {
    // Spring Core
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-mail'   // 메일

    // DB & Migration
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client:3.4.1'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly("org.flywaydb:flyway-mysql:11.14.1")
    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // queryDsl
    implementation("io.github.openfeign.querydsl:querydsl-core:${queryDslVersion}")
    implementation("io.github.openfeign.querydsl:querydsl-jpa:$queryDslVersion")
    annotationProcessor("io.github.openfeign.querydsl:querydsl-apt:$queryDslVersion:jpa")

    // OpenAPI (Swagger UI)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'


    // Jackson JSR-310 (Java Time)
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

    // Utils
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'com.github.f4b6a3:ulid-creator:5.2.2'   // ULID
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor' // 설정 메타데이터

    // Test (Unit Test)
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:5.13.0'
    testImplementation 'org.assertj:assertj-core:3.26.3'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(querydslSrcDir))
}

sourceSets {
    main {
        java {
            srcDirs += querydslSrcDir
        }
    }
}
configurations {
    querydsl.extendsFrom compileClasspath
}
